buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:1.2.3"
        classpath 'org.apache.commons:commons-lang3:3.3.2'
        classpath 'org.robolectric:robolectric-gradle-plugin:1.1.0'
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

allprojects {
    repositories {
        flatDir {
            dirs 'aars'
        }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

coveralls {
    jacocoReportPath = "$buildDir/../reports/jacoco/merged.xml"
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://jitpack.io' }
}

android {
    compileSdkVersion 22
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "org.smartregister.path"
        minSdkVersion 18
        targetSdkVersion 21
        versionCode 8
        versionName "1.2.2"
        multiDexEnabled true
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", 'opensrp_url', '"http://zeir.smartregister.org/opensrp"'
            buildConfigField "String", 'OPENMRS_URL', '"http://openmrs.zeir.smartregister.org/openmrs"'
            buildConfigField "String", 'OPENMRS_IDGEN_URL', '"/module/idgen/exportIdentifiers.form"'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_INITIAL_BATCH_SIZE", '250'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_BATCH_SIZE", '100'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_SOURCE", '1'
            buildConfigField "int", "VACCINE_SYNC_TIME", '12'
            buildConfigField "int", "DATABASE_VERSION", '8'
            buildConfigField "long", "MAX_SERVER_TIME_DIFFERENCE", "1800000l"
            buildConfigField "boolean", "TIME_CHECK", "true"
        }

        debug {
            resValue "string", 'opensrp_url', '"http://46.101.51.199:8080/opensrp"'
            buildConfigField "String", 'OPENMRS_URL', '"http://46.101.51.199:8080/openmrs"'
            buildConfigField "String", 'OPENMRS_IDGEN_URL', '"/module/idgen/exportIdentifiers.form"'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_INITIAL_BATCH_SIZE", '250'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_BATCH_SIZE", '100'
            buildConfigField "int", "OPENMRS_UNIQUE_ID_SOURCE", '2'
            buildConfigField "int", "VACCINE_SYNC_TIME", '0'
            buildConfigField "int", "DATABASE_VERSION", '8'
            buildConfigField "long", "MAX_SERVER_TIME_DIFFERENCE", "1800000l"
            buildConfigField "boolean", "TIME_CHECK", "false"
            testCoverageEnabled true
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'LICENSE.txt'
    }


    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile(name: 'android-json-form-wizard-debug', ext: 'aar')
    compile(name: 'opensrp-app-debug', ext: 'aar') {
        transitive = true
        exclude group: 'com.github.johnkil.print', module: 'print'
        exclude group: 'com.github.bmelnychuk', module: 'atv'
    }
    compile(name: 'opensrp-growth-monitoring-debug', ext: 'aar')

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.android.support:leanback-v17:22.2.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    compile 'com.jjoe64:graphview:4.2.1'
    compile 'com.android.support:support-v4:22.2.1'
    compile 'id.zelory:compressor:1.0.4'
    compile 'com.twotoasters.SectionCursorAdapter:library:1.0.+'
    compile 'com.github.ybq:Android-SpinKit:1.1.0'
    compile 'junit:junit:4.12'

    // opensrp-app dependencies -- TO REMOVE
    compile 'net.zetetic:android-database-sqlcipher:3.5.6@aar'
    compile 'org.ei.drishti:drishti-interface:0.1-SNAPSHOT'
    compile 'com.google.code.gson:gson:2.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.1.1'
    compile group: 'com.google.guava', name: 'guava', version:'15.0'
    compile group: 'commons-io', name: 'commons-io', version:'2.4'
    compile 'org.apache.commons:commons-lang3:3.2'
    compile 'joda-time:joda-time:2.3'
    compile 'com.mcxiaoke.volley:library:1.0.+'
    compile 'org.mozilla:rhino:1.7R4'
    compile fileTree(include: ['*.jar'], dir: 'libs')

    //android-native-form libraries -- TO REMOVE
    compile group: 'com.github.bmelnychuk', name: 'atv', version: '1.2.9'
    compile 'com.github.johnkil.print:print:1.3.1'
    compile 'com.github.rey5137:material:1.1.0'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile('com.github.riadhnet:MaterialSpinner:1.2@aar') {
        exclude group: 'com.nineoldandroids', module: 'library'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    //opensrp-growth-monitoring library -- TO REMOVE
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.4'
    compile 'com.github.lecho:hellocharts-android:v1.5.8'


    testCompile('org.robolectric:robolectric:3.4-rc3') {

        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.apache.maven', module: 'maven-model'
        exclude group: 'org.apache.maven', module: 'maven-ant-tasks'
        exclude group: 'com.android.support'
        exclude group: 'com.thoughtworks.xstream', module: 'xstream'
    }

    testCompile("org.robolectric:shadows-multidex:3.4-rc2") {
        exclude group: 'com.google.guava', module: 'guava'
    }

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.+') {

        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'junit', module: 'junit'
    }
    // PowerMock
    def powerMockVersion = '1.6.4'
    testCompile "org.powermock:powermock-module-junit4:$powerMockVersion"
    testCompile "org.powermock:powermock-module-junit4-rule:$powerMockVersion"
    testCompile "org.powermock:powermock-api-mockito:$powerMockVersion"
    testCompile("org.powermock:powermock-classloading-xstream:$powerMockVersion") {

        exclude group: 'com.thoughtworks.xstream', module: 'xstream'
    }

    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebug']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    getReports().getXml().setDestination(file("$buildDir/../reports/jacoco/merged.xml"))
    getReports().getHtml().setDestination(file("$buildDir/../reports/jacoco/html"))

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$ViewBinder*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebug.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}
